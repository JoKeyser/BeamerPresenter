# Maintainer: Valentin Bruch <software@vbruch.eu>
# This PKGBUILD file is more complicated than the one in the AUR to provide
# more precise dependency version checks for each build.
# Select the Qt version
: ${_qt_version_major:=6}  # 5 or 6

_qt_version_minor="$(pacman -Q "qt${_qt_version_major}-base" | sed -E 's/^qt[56]-base\s*[56]\.([0-9]+)\..*$/\1/')"
_qt_min_version="${_qt_version_major}.${_qt_version_minor}.0"
_qt_max_version="${_qt_version_major}.$(( ${_qt_version_minor} + 1 )).0"

pkgname='beamerpresenter-poppler'
pkgver='0.2.2_alpha'
pkgrel=1
pkgdesc="Modular multi-screen pdf presenter (using Poppler)"
arch=('x86_64')
url="https://github.com/stiglers-eponym/BeamerPresenter"
license=('GPL3')
makedepends=('cmake' 'git')

depends=("qt${_qt_version_major}-multimedia>=${_qt_min_version}" "qt${_qt_version_major}-multimedia<${_qt_max_version}" "poppler-qt${_qt_version_major}")
optdepends=('gst-libav: show videos' 'gst-plugins-good: show videos' 'hicolor-icon-theme: icons for action buttons' "qt${_qt_version_major}-svg: icons for tool buttons")

conflicts=('beamerpresenter' 'beamerpresenter-git' 'beamerpresenter-mupdf')
backup=('etc/xdg/beamerpresenter/beamerpresenter.conf' 'etc/xdg/beamerpresenter/gui.json')
source=('git://github.com/stiglers-eponym/BeamerPresenter.git')
sha256sums=('SKIP')

build() {
    cmake \
        -B "${pkgname}-${pkgver}/build" \
        -S "${srcdir}/BeamerPresenter" \
        -DCMAKE_BUILD_TYPE=Release \
        -DUSE_POPPLER=ON \
        -DUSE_MUPDF=OFF \
        -DQT_VERSION_MAJOR=${_qt_version_major} \
        -DCREATE_SHARED_LIBRARIES=OFF \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_INSTALL_SYSCONFDIR=/etc
    make -C "${pkgname}-${pkgver}/build" all
}

package() {
    make -C "${pkgname}-${pkgver}/build" DESTDIR="${pkgdir}" install
}
